import os
import shutil
from snakemake.utils import min_version
from snakemake.utils import validate


# Settings --------------------------------------------------------------------

min_version("6.3.0")

shell.executable("bash")


configfile: os.path.join(workflow.basedir, "..", ".tests", "config", "config.yaml")


# Validating samples in rules/common


workdir: config["workdir"]


report: "report/workflow.rst"


# Input rule ------------------------------------------------------------------


include: "rules/common.smk"


rule all:
    input:
        expand("{sample}/reports/{sample}_report.html", sample=samples.index),
        "reports/report.html",

rule benchmark:
    input:
        expand("{sample}/reports/{sample}_report.html", sample=samples.index),
        "reports/report.html",
        


# Includes --------------------------------------------------------------------


include: "rules/trimming.smk"
include: "rules/vsearch.smk" if config["cluster"]["method"] == "otu" else "rules/dada2.smk"
include: "rules/blast.smk"
include: "rules/reports.smk"
include: "rules/benchmarking.smk"


# Workflow --------------------------------------------------------------------


onstart:
    print(f"\nYou are using FooDMe version: {version}")
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline started\n")


onsuccess:
    try:
        for logfile in os.listdir(".snakemake/log/"):
            shutil.move(os.path.join(".snakemake/log", logfile), "logs")
        shutil.rmtree(".snakemake", ignore_errors=True)
    except:
        # if not executing .snakemake from workdir,
        # the log file will be in execution directory
        # as far as I know, there is now way to access this form here
        pass
    print("\nWorkflow finished, no error")
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline succesfully finished\n")


onerror:
    print(
            f"\nAn error occured, please consider filing an bug report."
            f""
            f"Issue tracker: https://github.com/CVUA-RRW/FooDMe/issues"
        )
    with open(pipe_log, "a") as f:
        f.write(f"[{get_local_time()}]: Pipeline stopped on error\n")
